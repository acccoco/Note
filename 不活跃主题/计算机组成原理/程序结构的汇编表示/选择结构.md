
## if 语句的实现

#### 基本方法
对于一个一般性的 if-else 语句：
```c
if (test_expr)
    then_statement;
else
	else_statement;
```

会将其转换为 goto 的结构：
其中：`**if() goto xxx**` 的结构可以直接转换为「**条件跳转**」指令。
```c
  if (!test_expr) goto false;			// 相当于条件跳转
  then_statement;
  goto done;
false:
  else_statement;
done:
```


#### 例子
以如下的代码片段为例：
```c
int get_less(int p1, int p2) {
    if (p1 > p2)
        return p2;
    else 
        return p1;
}
```

其对应的汇编片段为：
```c
    # 从栈中取出参数
    movl     8(%ebp), %eax      # p1 -> eax
    movl     12(%ebp), %edx     # p2 -> edx

    # 函数执行
    cmpl     %edx, %eax         # p1 - p2，注意减法顺序
    jle      .L1                
    movl     %edx, %eax         # p1 > p2，则 return p2
    jmp      .L2
.L1:
    movl     %eax, %eax         # p1 <= p2，则 return p1
.L2
```


## switch 语句
switch 语句可以通过「**if-else**」来实现，对于分支条件差异较小的情况，可以使用「**跳转表**」实现。

**跳表方式实现**：
```c
int switch_test(int a, int b, int c) {
	int result;
    
    switch (a) {
        case 15:            // .L1 分支
            c = b & 0x0f;
        case 10:            // .L2 分支
            result = c + 50;
            break;
        case 12:            // .L3 分支
        case 17:
            result = b + 50;
            break;
        case 14:            // .L4 分支
            result = b;
            break;
        default:            // .L5 分支
            result = a;
    }
    
    return result;
}
```

各个分支条件与分支的对应关系：
![[不活跃主题/计算机组成原理/程序结构的汇编表示/_attachments/1634643694427-0605051b-7b47-4312-8168-531e2ca0c3a9.png | 500]]

可以建立一个跳转表，以 `a-10` 的值作为索引，表项是分支的地址：
```c
.L7:
	.section  .rodata
    .align  4
.L8
	.long  .L2    # 对应 a = 10
    .long  .L5    # 对应 a = 11
    .long  .L3    # 对应 a = 12
    .long  .L5    # 对应 a = 13
    .long  .L4    # 对应 a = 14
    .long  .L1    # 对应 a = 15
    .long  .L5    # 对应 a = 16
    .long  .L3    # 对应 a = 17
```

最终的函数对应的汇编语句为：
```c
# 从栈中取出参数
  movl  8(%ebp), %eax    # a -> eax

# 为跳转做准备
  subl  $10, %eax
  cmpl  $7, %eax
  ja    .L5                # a>17 和 a<10 到默认分支；a<10 对应无符号正数
  jmp   *.L8(, %eax, 4)    # 以 (a-10) 为作引查询跳转表

.L1:                       # case 15
  movl  12(%ebp), %eax
  andl  $15, %eax
  movl  %eax, 16(%ebp)
.L2:                       # case 10
  movl  16(%ebp), %eax
  addl  $50, %eax
  jmp   .L7                # break

.L3:                       # case 12, case 17
  movl  12(%ebp), %eax
  addl  $50, %eax
  jmp   .L7                # break
 
.L4:                       # case 14
  movl  12(%ebp), %eax
  jmp   .L7                # break
      
.L5:                       # default
  addl  $10, %eax

.L7:
  ... 跳转表
```
